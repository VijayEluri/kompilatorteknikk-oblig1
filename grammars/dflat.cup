package dflat.parser;
import java_cup.runtime.*;
import dflat.syntaxtree.*;
import java.util.*;

parser code {:
:};



/* Terminals */

terminal String		NAME;
terminal String		STRING_LITERAL, DEC_INT_LITERAL, FLOAT_LITERAL;
terminal 			BOOLEAN, INTEGER, STRING, FLOAT;
terminal 			CLASS, FUNC, VAR, RET, REF;
terminal			IF, THEN, ELSE;
terminal			WHILE, DO, ASSIGN;
terminal			RETURN, NEW;
terminal			SEMI;
terminal			RBRACK, LBRACK, RPARENT, LPARENT;
terminal			EXCLAM, PERIOD, COMMA;
terminal			LT, GT, LTE, GTE, EQUAL, NOT_EQUAL;
terminal			TRUE, FALSE;
terminal			AND, OR;
terminal			PLUS, MINUS, DIVIDE, MULTIPLY, EXPONENT;
terminal			STATEMENT; //FIXME


/* Non terminals */
non terminal	Program	        program;
non terminal	Decl			decl;
non terminal	FuncDecl		func_decl;
non terminal	VarDecl			var_decl;
non terminal 	ClassDecl		class_decl;
non terminal	Type		    type;
non terminal	Type			return_type;
non terminal	Param			param;
non terminal	List<Decl>   	decl_list;
non terminal	List<Decl>		func_decl_list;
non terminal 	List<Param>		param_list;
non terminal 	List<Param>		param_decl;
non terminal	List<VarDecl>	var_decl_list;
non terminal	List<VarDecl> 	class_var_decl;
non terminal	List<Statement> func_stmt_list;
non terminal	List<Statement> stmt_list;
non terminal	Statement		stmt;
non terminal	Expression		exp;
non terminal 	VariableExpression var;
non terminal	Literal			literal;
non terminal	ActualParam		actual_param;
non terminal	List<ActualParam> actual_param_list;
non terminal	CallStatement	call_statement;
non terminal	LogOpExpression	log_op_expression;



precedence left OR;
precedence left AND;
precedence left EXCLAM;
precedence left PERIOD;
/* The grammar */
program			::= decl_list:dl {: RESULT= new Program(dl); :};
decl_list 		::= decl:d	{:
					List<Decl> declist = new LinkedList<Decl>();
					declist.add(d);
					RESULT = declist;
				:} 
				| decl_list:dl decl:d {:
					dl.add(d);
					RESULT = dl;
				:};
				
				
decl 			::= var_decl:vd {: RESULT = vd; :} 
				| class_decl:cd {: RESULT = cd; :}
				| func_decl:fd  {: RESULT = fd; :};
				


class_decl 		::= CLASS NAME:name LBRACK class_var_decl:vdl RBRACK {: RESULT = new ClassDecl(name, vdl); :};

func_decl		::= FUNC return_type:rt NAME:name LPARENT param_decl:pl RPARENT LBRACK func_decl_list:dl func_stmt_list:fsl RBRACK 
				    {: RESULT = new FuncDecl(name, pl, rt, dl, fsl); :};

func_decl_list ::= decl_list:dl {: RESULT = dl; :} | {: RESULT = new LinkedList<Decl>(); :};
func_stmt_list ::= stmt_list:sl {: RESULT = sl; :} | {: RESULT = new LinkedList<Statement>(); :};

param_decl		::= param_list:pl {: RESULT = pl; :} | {: RESULT = new ArrayList<Param>();:};

param_list		::= param_list:pl param:p {: pl.add(p); RESULT = pl;:} 
				|  param:p {:
					List<Param> pl = new ArrayList<Param>();
					pl.add(p);
					RESULT = pl;
				:};
				
param			::= REF type:t NAME:name {: RESULT = new Param(true, t, name);:} | type:t NAME:name {: RESULT = new Param(false, t, name);:};

return_type		::= RET type:t {: RESULT = t; :} | {: RESULT = null; :};

class_var_decl 	::= var_decl_list:vdl {: RESULT = vdl; :} | {: RESULT = new ArrayList<VarDecl>();:};

var_decl_list 	::= var_decl_list:vdl var_decl:d {: vdl.add(d);RESULT = vdl; :} 
				| var_decl:d {: List<VarDecl> vdl = new LinkedList<VarDecl>(); vdl.add(d); RESULT = vdl; :}; 

var_decl 		::= VAR type:t NAME:name SEMI
					{: 
						System.out.println("new node");
						RESULT = new VarDecl(t, new Name(name)); 
					:};
/* FIXME */
type			::= BOOLEAN {: RESULT = new BooleanType(); :}  
				  | FLOAT {: RESULT = new FloatType(); :} 
				  | INTEGER {: RESULT = new IntegerType(); :} 
				  | STRING {: RESULT = new StringType(); :} 
				  | NAME:name {: RESULT = new ClassType(new Name(name)); :};
				  
stmt_list 		::= stmt_list:sl stmt:s {: sl.add(s); RESULT = sl;:} | stmt:s {: List<Statement> sl = new ArrayList<Statement>(); sl.add(s); RESULT = sl;:};
stmt		::= exp:e SEMI {: RESULT = new ReturnStatement(e); :}; //TODO

exp				::=  LPARENT exp:e RPARENT {: RESULT = e;:} 
				| literal:l {: RESULT = l;:} 
				| var:v {: RESULT = v;:}
				| EXCLAM exp:e {: RESULT = new NegatedExpression(e); :}
				| call_statement:s {: RESULT = s; :}
				| NEW type:t LPARENT RPARENT {: RESULT = new NewExpression(t);:}
				| log_op_expression:op {: RESULT = op;:};
				
//FIXME, gj¿r om dette til AndExpression og OrExpression
log_op_expression ::= exp:e1 AND exp:e2 {: RESULT = new LogOpExpression(e1, new AndOp(), e2); :} | exp:e1 OR exp:e2 {: RESULT = new LogOpExpression(e1, new OrOp(), e2);:};


var 			::= NAME:name {: RESULT = new VariableExpression(new Name(name)); :}
				|  exp:e PERIOD NAME:name {: RESULT = new ObjectVariableExpression(e, new Name(name)); :};

				
call_statement ::= NAME:name LPARENT actual_param_list:pl RPARENT {: RESULT = new CallStatement(new Name(name), pl); :} 
				|NAME:name LPARENT RPARENT {: RESULT = new CallStatement(new Name(name), new ArrayList<ActualParam>()); :} ;

actual_param_list ::= actual_param_list:pl COMMA actual_param:p 
					{: pl.add(p); RESULT = pl; :}
					| actual_param:p 
					{: List<ActualParam> pl = new LinkedList<ActualParam>();
					   pl.add(p);
					   RESULT = pl;
					:};
actual_param ::= REF var:v {: RESULT = new PassByReferenceParam(v); :} | exp:e {: RESULT = new PassByValueParam(e);:};
				
literal			::= DEC_INT_LITERAL:i {: RESULT = new IntLiteral(i); :}
				  | STRING_LITERAL:s {: RESULT = new StringLiteral(s); :}
				  | FLOAT_LITERAL:f {: RESULT = new FloatLiteral(f); :}
				  | TRUE {: RESULT = new BooleanLiteral(true); :} | FALSE {: RESULT = new BooleanLiteral(false);:} ;




						